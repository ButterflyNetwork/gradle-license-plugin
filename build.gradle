plugins {
  id "com.gradle.build-scan" version "1.10.3"
  id "com.github.kt3k.coveralls" version "2.8.2"
  id "com.jfrog.artifactory" version "4.4.18"
  id "com.jfrog.bintray" version "1.8.0"
  id "com.github.ben-manes.versions" version "0.17.0"
  id "com.gradle.plugin-publish" version "0.9.9"
  id "org.jetbrains.kotlin.jvm" version "1.2.0"
  id "org.jetbrains.dokka" version "0.9.14"
  id "java-gradle-plugin"
  id "groovy"
  id "maven-publish"
  id "jacoco"
}

repositories {
  jcenter()
  google()
}

apply from: "gradle/dependencies.gradle"

group = GROUP
version = VERSION_NAME
description = POM_DESCRIPTION

sourceCompatibility = ext.javaVersion
targetCompatibility = ext.javaVersion

dependencies {
  implementation localGroovy()
  implementation deps.kotlinStdlib

  compileOnly deps.gradle

  testImplementation deps.gradle
  testImplementation deps.kotlinGradlePlugin
  testImplementation deps.spockCore, { exclude module: "groovy-all" } // Use localGroovy()
}

// Ordering matters
apply from: "gradle/scan.gradle"
apply from: "gradle/compile.gradle"
apply from: "gradle/plugin.gradle"
apply from: "gradle/publish.gradle"
apply from: "gradle/quality.gradle"
apply from: "gradle/wrapper.gradle"

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
  kotlinOptions {
    jvmTarget = rootProject.ext.javaVersion
  }
}

// Compile Kotlin then Groovy
afterEvaluate {
  compileKotlin.dependsOn = compileKotlin.taskDependencies.values - "compileJava"
  compileGroovy.dependsOn compileKotlin
  compileGroovy.classpath += files(compileKotlin.destinationDir)
  classes.dependsOn compileGroovy
}
